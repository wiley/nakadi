buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.yaml:snakeyaml:1.21'
        classpath 'org.owasp:dependency-check-gradle:5.3.2.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'project-report'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'

group 'org.zalando'

sourceSets {
    acceptanceTest {
        java {
            srcDir file('src/acceptance-test/java')
        }
        resources.srcDir file('src/acceptance-test/resources')
    }
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude group: "com.vaadin.external.google", module: "android-json"
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
}

dependencies {
    // Override spring-boot BOM versions
    ext['json.version'] = '20180130'
    ext['json-path'] = '2.4.0'
    ext['jsonassert'] = '1.5.0'

    implementation project(':core-common')
    implementation project(':core-metastore')
    implementation project(':core-services')

    implementation "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    implementation project(':api-publishing')
    implementation project(':api-consumption')
    implementation project(':api-metastore')
    implementation project(':api-cursors')
    implementation project(':api-misc')
    implementation project(':app')

    // spring
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'logback-classic'
        exclude module: 'log4j-over-slf4j'
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation "org.springframework:spring-webmvc"
    implementation "org.springframework.boot:spring-boot-starter-jetty"

    implementation('org.springframework.boot:spring-boot-starter-security') {
        exclude module: "logback-classic"
    }

    implementation "org.apache.kafka:kafka-clients:$kafkaClientVersion"

    implementation 'org.slf4j:slf4j-log4j12'

    implementation "org.springframework.boot:spring-boot-starter-json"
    implementation 'org.zalando:jackson-datatype-problem:0.22.0'
    implementation 'org.zalando:problem:0.22.0'
    implementation 'org.json:json:20180130'
    implementation 'software.amazon.msk:aws-msk-iam-auth:1.1.4'

    implementation('org.zalando.stups:stups-spring-oauth2-server:1.0.24') {
        exclude module: "httpclient"
    }

    // tests
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.5.2') {
        exclude module: "hamcrest-core"
    }
    testImplementation "org.springframework:spring-test"
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.skyscreamer:jsonassert'
    testImplementation 'uk.co.datumedge:hamcrest-json:0.2'
    testImplementation('com.jayway.restassured:rest-assured:2.9.0') {
        exclude module: "hamcrest-core"
        exclude module: "hamcrest-library"
    }
    testImplementation 'com.jayway.jsonpath:json-path'
    testRuntime 'org.pegdown:pegdown:1.6.0'
    testImplementation project(path: ':core-common', configuration: 'testOutput')
    testImplementation project(path: ':core-services', configuration: 'testOutput')

    testCompile 'junit:junit:4.13.1'
}

task acceptanceTest(type: Test) {
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
}

acceptanceTest {
    testLogging {
        events "passed", "skipped", "failed"
    }
    testLogging.exceptionFormat = 'full'
    maxParallelForks = 1
}

bootJar {
    enabled = false
}
jar {
    enabled = true
}
