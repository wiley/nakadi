buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'org.yaml:snakeyaml:1.21'
        classpath 'org.owasp:dependency-check-gradle:5.3.2.1'
    }
}

plugins {
    id "com.github.davidmc24.gradle.plugin.avro" version "1.3.0"
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'project-report'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: "com.github.davidmc24.gradle.plugin.avro-base"

group 'org.zalando'

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude group: "com.vaadin.external.google", module: "android-json"
}

dependencies {
    // Override spring-boot BOM versions
    ext['json.version'] = '20180130'
    ext['json-path'] = '2.4.0'
    ext['jsonassert'] = '1.5.0'

    // spring
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'logback-classic'
        exclude module: 'log4j-over-slf4j'
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation "org.springframework.boot:spring-boot-starter-jetty"

    // oauth
    implementation('org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE') {
        exclude module: 'spring-webmvc'
        exclude module: 'spring-security-config'
        exclude module: 'spring-beans'
        exclude module: 'spring-core'
        exclude module: 'spring-context'
    }
    implementation('org.springframework.boot:spring-boot-starter-security') {
        exclude module: "logback-classic"
    }

    implementation 'org.zalando:nakadi-plugin-api:3.2.1'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'software.amazon.msk:aws-msk-iam-auth:1.1.4'

    implementation "org.springframework.boot:spring-boot-starter-json"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
    implementation 'org.zalando:jackson-datatype-problem:0.22.0'
    implementation 'org.zalando:problem:0.22.0'
    implementation 'org.json:json:20180130'

    implementation 'org.slf4j:slf4j-log4j12'

    // open tracing
    implementation "io.opentracing:opentracing-api:$opentracingVersion"
    implementation "io.opentracing:opentracing-util:$opentracingVersion"

    implementation "org.apache.kafka:kafka-clients:$kafkaClientVersion"

    implementation("org.apache.curator:curator-recipes:$curatorVersion") {
        exclude module: "zookeeper"
    }
    implementation("org.apache.zookeeper:zookeeper:$zookeeperVersion") {
        exclude module: "log4j"
    }

    implementation 'org.echocat.jomon:runtime:1.6.3'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.7'

    implementation "io.dropwizard.metrics:metrics-core:$dropwizardVersion"
    implementation("com.ryantenney.metrics:metrics-spring:$dropwizardVersion") {
        exclude module: 'spring-core'
    }
    implementation "io.dropwizard.metrics:metrics-servlets:$dropwizardVersion"
    implementation "io.dropwizard.metrics:metrics-jvm:$dropwizardVersion"

    implementation("org.springframework.boot:spring-boot-starter-jdbc") {
        exclude module: 'HikariCP'
    }
    implementation 'org.apache.tomcat:tomcat-jdbc'
    implementation 'org.postgresql:postgresql:42.3.2'

    implementation('commons-beanutils:commons-beanutils:1.9.4')
    implementation('com.github.everit-org.json-schema:org.everit.json.schema:1.8.0') {
        exclude module: "json"
    }
    implementation 'org.zalando:problem-spring-web:0.23.0'

    // avro
    implementation "org.apache.avro:avro:1.11.0"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-avro:2.8.5"

    implementation 'com.github.luben:zstd-jni:1.5.2-2'

    // tests
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation("org.springframework.boot:spring-boot-starter-validation")
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.5.2') {
        exclude module: "hamcrest-core"
    }
    testImplementation "org.springframework:spring-test"
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.skyscreamer:jsonassert'
    testImplementation 'uk.co.datumedge:hamcrest-json:0.2'
    testImplementation('com.jayway.restassured:rest-assured:2.9.0') {
        exclude module: "hamcrest-core"
        exclude module: "hamcrest-library"
    }
    testImplementation 'com.jayway.jsonpath:json-path'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.pegdown:pegdown:1.6.0'
}
// end::dependencies[]

configurations {
    testOutput.extendsFrom(testCompile)
}

task jarTest(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
    classifier = 'test'
}

artifacts {
    testOutput jarTest
}

bootJar {
    enabled = false
}
jar {
    enabled = true
}

import com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
    source("src/main/resources/avro-schema")
    outputDir = file("build/generated/sources")
}

tasks.named("compileJava").configure {
    source(generateAvro)
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "build/generated/sources"]
        }
    }
}
